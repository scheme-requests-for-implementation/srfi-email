<!-- MHonArc v2.6.15 -->
<!--X-Subject: a preface -->
<!--X-From-R13: fbb <gvyqrNgvyqr.pb.xe> -->
<!--X-Date: Tue, 23 Mar 2004 17:18:40 +0100 (NFT) -->
<!--X-Message-Id: 87isgvv8nc.fsf@tilde.co.kr -->
<!--X-Content-Type: text/plain -->
<!--X-Head-End-->
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
        "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>a preface</title>
</head>
<body>
<!--X-Body-Begin-->
<!--X-User-Header-->
<!--X-User-Header-End-->
<!--X-TopPNI-->
<hr>
[<a href="msg00000.html">Date Prev</a>][<a href="msg00002.html">Date Next</a>][<a href="msg00015.html">Thread Prev</a>][<a href="msg00003.html">Thread Next</a>][<a href="maillist.html#00001">Date Index</a>][<a href="threads.html#00001">Thread Index</a>]
<!--X-TopPNI-End-->
<!--X-MsgBody-->
<!--X-Subject-Header-Begin-->
<h1>a preface</h1>
<p style="max-width: 30em;">This page is part of the web mail archives of <a href="http://srfi.schemers.org/srfi-54">SRFI 54</a> from before July 7th, 2015.  The <a href="http://srfi-email.schemers.org/srfi-54">new archives</a> for SRFI 54 contain all messages, not just those from before July 7th, 2015.</p>
<hr>
<!--X-Subject-Header-End-->
<!--X-Head-of-Message-->
<ul>
<li><em>To</em>: <a href="mailto:srfi-54@DOMAIN.HIDDEN">srfi-54@xxxxxxxxxxxxxxxxx</a></li>
<li><em>Subject</em>: a preface</li>
<li><em>From</em>: soo &lt;<a href="mailto:tilde@DOMAIN.HIDDEN">tilde@xxxxxxxxxxx</a>&gt;</li>
<li><em>Date</em>: 24 Mar 2004 01:19:03 +0900</li>
<li><em>Delivered-to</em>: <a href="mailto:srfi-54@DOMAIN.HIDDEN">srfi-54@xxxxxxxxxxxxxxxxx</a></li>
<li><em>Sender</em>: soo &lt;<a href="mailto:soo@DOMAIN.HIDDEN">soo@xxxxxxxxxxxxxxxxx</a>&gt;</li>
<li><em>User-agent</em>: Gnus/5.09 (Gnus v5.9.0) Emacs/21.2</li>
</ul>
<!--X-Head-of-Message-End-->
<!--X-Head-Body-Sep-Begin-->
<hr>
<!--X-Head-Body-Sep-End-->
<!--X-Body-of-Message-->
<pre>Hi.
The current uploaded draft is old one.
I submitted several revised proposals before drafting, but there was no
response. (I have heard that Solsona had some problems with his machine.)  
The following is my last revised proposal.

Title

Formatting

Author

Joo ChurlSoo

Abstract

This SRFI introduces the FMT procedure that converts any object to a string.
Unlike the procedure called FORMAT, this FMT procedure takes one object as the
first argument and accepts several optional arguments.

Rationale

The FMT procedure provides a handy optional and functional interface.

Specification

(FMT &lt;number&gt; [[&lt;width&gt;] [&lt;depth&gt;] [&lt;char&gt;] [&lt;radix&gt;] [&lt;plus&gt;] [&lt;exactness&gt;]
	       [&lt;space&gt;] [&lt;string&gt;] ...])
(FMT &lt;others&gt; [[&lt;width&gt;] [&lt;count&gt;] [&lt;char&gt;] [&lt;show&gt;] [&lt;case&gt;] [&lt;space&gt;]
	       [&lt;string&gt;] ...]) 

     * &lt;number&gt; is any numeric expression.
     * &lt;others&gt; are any expressions except number.
     * &lt;width&gt; is an exact integer whose absolute value specifies the width of
       the resulting string.  When the resulting string has fewer characters
       than the absolute value of &lt;width&gt;, it is padded with &lt;char&gt;s, either
       on the left if &lt;width&gt; is positive, or on the right if &lt;width&gt; is
       negative.  On the other hand, when the resulting string has more
       characters than the absolute value of &lt;width&gt;, the &lt;width&gt; is ignored.
     * &lt;depth&gt; is a non-negative exact integer that specifies the number of
       decimal digits after decimal point.
     * &lt;count&gt; is a non-negative exact integer that specifies the number of
       characters of the resulting string.
     * &lt;char&gt; is a padding character.
     * &lt;radix&gt; is a symbol: b (binary), d (decimal), o (octal), x (hexadecimal)
     * &lt;show&gt; is a procedure: display, write
     * If &lt;plus&gt; is a procedure + and &lt;number&gt; is a positive number without a
       positive sign, the positive sign is prefixed to the &lt;number&gt;.
     * &lt;case&gt; is a symbol: u (upcase), d (downcase), t (titlecase)
     * &lt;exactness&gt; is a symbol: e (exact), i (inexact)
     * &lt;space&gt; is a list whose elements are non-negative exact integer, and
       the number of elements of the list is 1 or 2.  The resulting string is
       padded with space character on the left as much as the value of the
       first element and on the right as much as th value of the second
       element regardless of &lt;width&gt;.
     * &lt;string&gt;s are strings that are appended to the resulting string.

The order of optional arguments is ignored except that &lt;depth&gt; or &lt;count&gt; can
be defined only after &lt;width&gt; is defined.

Examples

(fmt 129.995)				&quot;129.995&quot;
(fmt 129.995 '(2))			&quot;  129.995&quot;
(fmt 129.995 '(1 1))			&quot; 129.995 &quot;
(fmt 129.995 10 2)			&quot;    130.00&quot;
(fmt 129.995 -10 2)			&quot;130.00    &quot;
(fmt 129.995  10 #\0 2)			&quot;0000130.00&quot;
(fmt 129.995 #\0 10 + 2)		&quot;+000130.00&quot;
(fmt 129.995  #\0 10 + 2 'o 'e)		error
(fmt 129.995  #\0 10 + 'o 'e)		&quot;+000000202&quot;
(fmt 129.995  #\0 10 + 'o 'e '(1 2))	&quot; +000202  &quot;
(fmt 129.995  '(1) #\0 10 + 'o 'e)	&quot; +00000202&quot;
(fmt (sqrt -5) 10)			&quot;0.0+2.23606797749979i&quot;
(fmt (sqrt -5) 10 2)			&quot;0.00+2.24i&quot;
(fmt 3.14159e12 10 2 +)			&quot;  +3.14e12&quot;
(fmt #x123 'o 10)			&quot;       443&quot;
(fmt #x123 -10 3 + #\*)			&quot;+291.000**&quot;
(fmt &quot;string&quot;  10)			&quot;    string&quot;
(fmt &quot;string&quot;  '(1 1))			&quot; string &quot;
(fmt &quot;string&quot; -10 '(1 2))		&quot; string   &quot;
(fmt &quot;The number is 3.&quot; 't 10)		&quot;The Number Is 3.&quot;
(fmt &quot;The number is 3.&quot; 'u '(1))	&quot; THE NUMBER IS 3.&quot;
(fmt &quot;string&quot;  -10 write)		&quot;\&quot;string\&quot;  &quot;
(fmt &quot;string&quot; 10 3)			&quot;       str&quot;
(fmt &quot;string&quot; -10 3)			&quot;str       &quot;
(fmt &quot;string&quot; #\- -10 3)		&quot;str-------&quot;
(fmt #\a write)				&quot;#\\a&quot;
(fmt #\a display)			&quot;a&quot;
(fmt #\a 10)				&quot;         a&quot;
(fmt 'symbol 10)			&quot;    symbol&quot;
(fmt '(1 #\a &quot;str&quot; sym '(a)) write)	&quot;(1 #\\a \&quot;str\&quot; sym (quote (a)))&quot;
(fmt '(1 #\a &quot;str&quot; sym '(a)))		&quot;(1 a str sym (quote (a)))&quot;
(fmt '(1 #\a &quot;str&quot; sym '(a)) 10)	&quot;(1 a str sym (quote (a)))&quot;
(fmt '(1 #\a &quot;str&quot; sym '(a)) 10 10)	&quot;(1 a str s&quot;
(fmt #(1 #\a &quot;str&quot; sym '(a)) 10)	&quot;#(1 a str sym (quote (a)))&quot;
(fmt #(1 #\a &quot;str&quot; sym '(a)) 10 write)	&quot;#(1 #\\a \&quot;str\&quot; sym (quote (a)))&quot;
(fmt 123 &quot;is an integer.&quot; + 0 2 '(0 1))	&quot;+123.00 is an integer.&quot;
(fmt &quot;this&quot; 't (fmt &quot;is&quot; '(1 1)) &quot;A.&quot;)	&quot;This is A.&quot;

Implementation

The implementation below requires SRFI-1 (List library), SRFI-6 (Basic string
ports), SRFI-8 (Receive), SRFI-13 (String library), and SRFI-23 (Error
reporting mechanism).

(define (opt-values rest-list . default-list)
  (let loop ((rest-list rest-list)
	     (default-list default-list)
	     (result '()))
    (if (null? default-list)
	(apply values (append (reverse result) rest-list))
	(let ((default (car default-list)))
	  (let lp ((rest rest-list)
		   (head '()))
	    (if (null? rest)
		(loop (reverse head)
		      (cdr default-list)
		      (cons (car default) result))
		(if (list? default)
		    (if (member (car rest) default)
			(loop (append (reverse head) (cdr rest))
			      (cdr default-list)
			      (cons (car rest) result))
			(lp (cdr rest) (cons (car rest) head)))
		    (if ((cdr default) (car rest))
			(loop (append (reverse head) (cdr rest))
			      (cdr default-list)
			      (cons (car rest) result))
			(lp (cdr rest) (cons (car rest) head))))))))))

(define-syntax arg-or
  (syntax-rules()
    ((arg-or arg (a1 a2 ...) ...)
     (or (and (not (symbol? 'arg))
	      (error &quot;bad syntax&quot; 'arg '(symbol? 'arg)
		     '(arg-or arg (a1 a2 ...) ...)))
	 (and (a1 a2 ...)
	      (error &quot;incorrect argument&quot; arg 'arg '(a1 a2 ...)))
	 ...))
    ((arg-or caller arg (a1 a2 ...) ...)
     (or (and (not (symbol? 'arg))
	      (error &quot;bad syntax&quot; 'arg '(symbol? 'arg)
		     '(arg-or caller arg (a1 a2 ...) ...)))
	 (and (a1 a2 ...)
	      (if (string? caller)
		  (error caller arg 'arg '(a1 a2 ...))
		  (error &quot;incorrect argument&quot; arg 'arg '(a1 a2 ...) caller)))
	 ...))))

;; accessory macro for arg-ors
(define-syntax caller-arg-or
  (syntax-rules()
    ((caller-arg-or caller arg (a1 a2 ...) ...)
     (or (and (not (symbol? 'arg))
	      (error &quot;bad syntax&quot; 'arg '(symbol? 'arg)
		     '(caller-arg-or caller arg (a1 a2 ...) ...)))
	 (and (a1 a2 ...)
	      (if (string? caller)
		  (error caller arg 'arg '(a1 a2 ...))
		  (error &quot;incorrect argument&quot; arg 'arg '(a1 a2 ...) caller)))
	 ...))
    ((caller-arg-or null caller arg (a1 a2 ...) ...)
     (or (and (not (symbol? 'arg))
	      (error &quot;bad syntax&quot; 'arg '(symbol? 'arg)
		     '(caller-arg-or caller arg (a1 a2 ...) ...)))
	 (and (a1 a2 ...)
	      (if (string? caller)
		  (error caller arg 'arg '(a1 a2 ...))
		  (error &quot;incorrect argument&quot; arg 'arg '(a1 a2 ...) caller)))
	 ...))))

(define-syntax arg-ors
  (syntax-rules (common)
    ((arg-ors (a1 a2 ...) ...)
     (or (arg-or a1 a2 ...) ...))
    ((arg-ors common caller (a1 a2 ...) ...)
     (or (caller-arg-or caller a1 a2 ...) ...))))


(define (fmt expr . rest)
  (if (number? expr)
      (receive (width depth char radix plus exactness space . str-list)
	  (opt-values rest
		      (cons #f (lambda (x) (and (integer? x) (exact? x))))
		      (cons #f (lambda (x)
				 (and (integer? x) (exact? x) (&lt;= 0 x))))
		      (cons #f char?)
		      (list 'd 'b 'o 'x)
		      (cons #f (lambda (x) (eq? x +)))
		      (cons #f (lambda (x) (memq x '(e i))))
		      (cons #f (lambda (x)
				 (and (list? x)
				      (&lt;= 1 (length x) 2)
				      (every (lambda (x)
					       (and (integer? x)
						    (exact? x)
						    (&lt;= 0 x)))
					     x)))))
	(arg-ors (&quot;fmt: bad argument&quot;  str-list
		  (not (every string? str-list)))
		 (&quot;fmt: non-decimal cannot be inexact&quot; radix
		  (and (memq radix '(b o x))
		       (or depth
			   (and (inexact? expr) (not (eq? exactness 'e)))
			   (eq? exactness 'i))))
		 (&quot;fmt: exact number cannot have a decimal point&quot; depth
		  (and depth (eq? exactness 'e)))
		 (&quot;fmt: unnecessary padding character&quot; char
		  (and char (not width))))
	(let* ((width (or width 0))
	       (char (or char #\space))
	       (sign (if (&lt; width 0) '- '+))
	       (str (number-&gt;string
		     (if exactness
			 (if (eq? exactness 'e)
			     (if (inexact? expr) (inexact-&gt;exact expr) expr)
			     (if (exact? expr) (exact-&gt;inexact expr) expr))
			 (if (and depth (exact? expr))
			     (exact-&gt;inexact expr)
			     expr))
		     (cdr (assq radix '((b . 2) (d . 10) (o . 8) (x . 16))))))
	       (str
		(if depth
		    (let ((e-index (or (string-index str #\e)
				       (string-index str #\E)))
			  (+-index (string-index str #\+ 1)))
		      (define (mold str dep)
			(let ((len (string-length str))
			      (index (string-index str #\.)))
			  (if index
			      (let ((d-len (- len index 1)))
				(if (&lt;= d-len dep)
				    (string-append str
						   (make-string (- dep d-len)
								#\0))
				    (mold (number-&gt;string
					   (+ (string-&gt;number
					       (substring str 0
							  (+ (if (= dep 0) 0 1)
							     index dep)))
					      (if (&lt; 4 (string-&gt;number
							(string
							 (string-ref
							  str
							  (+ 1 index dep)))))
						  (expt 0.1 dep) 0)))
					  dep)))
			      (string-append str &quot;.&quot; (make-string dep #\0)))))
		      (cond
		       (e-index
			(string-append (mold (substring str 0 e-index) depth)
				       (substring str e-index
						  (string-length str))))
		       (+-index
			(string-append (mold (substring str 0 +-index) depth)
				       &quot;+&quot;
				       (mold (substring str (+ 1 +-index)
							(- (string-length str)
							   1)) depth)
				       (string (string-ref
						str
						(- (string-length str) 1)))))
		       (else
			(mold str depth))))
		    str))
	       (str (if (and (&lt; 0 (real-part expr))
			     (not (eqv? #\+ (string-ref str 0)))
			     plus)
			(string-append &quot;+&quot; str)
			str))
	       (len (string-length str))
	       (lt (if space (car space) 0))
	       (rt (if (and space (not (null? (cdr space)))) (cadr space) 0))
	       (pad (- (abs width) len lt rt)))
	  (apply string-append
		 (make-string lt #\space)
		 (cond
		  ((&lt;= pad 0) str)
		  ((eq? sign '+)
		   (if (and (eqv? char #\0)
			    (or (eqv? #\+ (string-ref str 0))
				(eqv? #\- (string-ref str 0))))
		       (string-append (string (string-ref str 0))
				      (make-string pad char)
				      (substring str 1 len))
		       (string-append (make-string pad char) str)))
		  (else
		   (string-append str (make-string pad char))))
		 (make-string rt #\space)
		 str-list)))
      (receive (width depth char show case space . str-list)
	  (opt-values rest
		      (cons #f (lambda (x) (and (integer? x) (exact? x))))
		      (cons #f (lambda (x)
				 (and (integer? x) (exact? x) (&lt;= 0 x))))
		      (cons #f char?)
		      (list display write)
		      (cons #f (lambda (x) (memq x '(d u t))))
		      (cons #f (lambda (x)
				 (and (list? x)
				      (&lt;= 1 (length x) 2)
				      (every (lambda (x)
					       (and (integer? x)
						    (exact? x)
						    (&lt;= 0 x)))
					     x)))))
	(arg-ors (&quot;fmt: bad argument&quot; str-list
		  (not (every string? str-list)))
		 (&quot;fmt: unnecessary padding character&quot; char
		  (and char (not width))))
	(let* ((width (or width 0))
	       (char (or char #\space))
	       (sign (if (&lt; width 0) '- '+))
	       (str (get-output-string
		     (let ((str-port (open-output-string)))
		       (show expr str-port)
		       str-port)))
	       (str (if (and depth (&lt; depth (string-length str)))
			(substring str 0 depth)
			str))
	       (str (if case
			((cdr (assq case `((d . ,string-downcase)
					   (u . ,string-upcase)
					   (t . ,string-titlecase)))) str)
			str))
	       (lt (if space (car space) 0))
	       (rt (if (and space (not (null? (cdr space)))) (cadr space) 0))
	       (pad (- (abs width) (string-length str) lt rt)))
	  (apply string-append
		 (make-string lt #\space)
		 (cond
		  ((&lt;= pad 0) str)
		  ((eq? sign '+) (string-append (make-string pad char) str))
		  (else (string-append str (make-string pad char))))
		 (make-string rt #\space)
		 str-list)))))

Copyright

Copyright (C) Joo ChurlSoo (2004). All Rights Reserved.

This document and translations of it may be copied and furnished to others, and
derivative works that comment on or otherwise explain it or assist in its
implementation may be prepared, copied, published and distributed, in whole or
in part, without restriction of any kind, provided that the above copyright
notice and this paragraph are included on all such copies and derivative works.
However, this document itself may not be modified in any way, such as by
removing the copyright notice or references to the Scheme Request For
Implementation process or editors, except as needed for the purpose of
developing SRFIs in which case the procedures for copyrights defined in the
SRFI process must be followed, or as required to translate it into languages
other than English.

The limited permissions granted above are perpetual and will not be revoked by
the authors or their successors or assigns.

This document and the information contained herein is provided on an &quot;AS IS&quot;
basis and THE AUTHOR AND THE SRFI EDITORS DISCLAIM ALL WARRANTIES, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO ANY WARRANTY THAT THE USE OF THE
INFORMATION HEREIN WILL NOT INFRINGE ANY RIGHTS OR ANY IMPLIED WARRANTIES OF
MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.

A Note:
I am on neither &quot;format strings are wrong&quot; side nor &quot;format strings are right&quot;
side, and indeed have no knowledge to infer it.  I am only a Scheme consumer.
I've used FORMAT because of its functions such as left and right padding of
numbers or strings and expressing the precision of number.  As I thought the
function of the current SRFI's FORMAT was a little insufficient (such as right
padding), and the `former' side had not provided a real thing for Scheme
users, I submitted this proposal.
While defining the new procedure (FMT), I felt that the problem was handling
its optional arguments, and REST-VALUES (a prodecure in SRFI-51 that I had
submitted) was insufficient and inappropriate.  So I appended the third mode
of operation to the function of REST-VALUES, and submitted the revised SRFI-51
to SRFI editors.  If the revised SRFI-51 is finalized, I'll substitute
REST-VALUES for OPT-VALUES which is a temporary and accessory procedure for
handling rest list (optional arguments).

Thanks.
-- 
INITTERM

</pre>
<!--X-Body-of-Message-End-->
<!--X-MsgBody-End-->
<!--X-Follow-Ups-->
<hr>
<ul><li><strong>Follow-Ups</strong>:
<ul>
<li><strong><a name="00003" href="msg00003.html">Re: a preface</a></strong>
<ul><li><em>From:</em> David Van Horn</li></ul></li>
<li><strong><a name="00005" href="msg00005.html">Re: a preface</a></strong>
<ul><li><em>From:</em> Francisco Solsona</li></ul></li>
</ul></li></ul>
<!--X-Follow-Ups-End-->
<!--X-References-->
<!--X-References-End-->
<!--X-BotPNI-->
<ul>
<li>Prev by Date:
<strong><a href="msg00000.html">Comments and some bugs</a></strong>
</li>
<li>Next by Date:
<strong><a href="msg00002.html">Re: Comments and some bugs</a></strong>
</li>
<li>Previous by thread:
<strong><a href="msg00015.html">Re: Comments and some bugs</a></strong>
</li>
<li>Next by thread:
<strong><a href="msg00003.html">Re: a preface</a></strong>
</li>
<li>Index(es):
<ul>
<li><a href="maillist.html#00001"><strong>Date</strong></a></li>
<li><a href="threads.html#00001"><strong>Thread</strong></a></li>
</ul>
</li>
</ul>

<!--X-BotPNI-End-->
<!--X-User-Footer-->
<!--X-User-Footer-End-->
</body>
</html>
