<!-- MHonArc v2.6.15 -->
<!--X-Subject: Re: Reference Implementation problem -->
<!--X-From-R13: Xbb QuheyEbb <gvyqrNgvyqr.pb.xe> -->
<!--X-Date: Wed, 17 Jan 2007 13:04:57 +0100 (MET) -->
<!--X-Message-Id: uhcup506a.fsf@initerm.com -->
<!--X-Content-Type: text/plain -->
<!--X-Head-End-->
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
        "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>Re: Reference Implementation problem</title>
</head>
<body>
<!--X-Body-Begin-->
<!--X-User-Header-->
<!--X-User-Header-End-->
<!--X-TopPNI-->
<hr>
[<a href="msg00004.html">Date Prev</a>][<a href="msg00006.html">Date Next</a>][<a href="msg00004.html">Thread Prev</a>][<a href="msg00006.html">Thread Next</a>][<a href="maillist.html#00005">Date Index</a>][<a href="threads.html#00005">Thread Index</a>]
<!--X-TopPNI-End-->
<!--X-MsgBody-->
<!--X-Subject-Header-Begin-->
<h1>Re: Reference Implementation problem</h1>
<hr>
<!--X-Subject-Header-End-->
<!--X-Head-of-Message-->
<ul>
<li><em>To</em>: <a href="mailto:srfi-54@DOMAIN.HIDDEN">srfi-54@xxxxxxxxxxxxxxxxx</a></li>
<li><em>Subject</em>: Re: Reference Implementation problem</li>
<li><em>From</em>: Joo ChurlSoo &lt;<a href="mailto:tilde@DOMAIN.HIDDEN">tilde@xxxxxxxxxxx</a>&gt;</li>
<li><em>Date</em>: 17 Jan 2007 21:04:45 +0900</li>
<li><em>Delivered-to</em>: <a href="mailto:srfi-54@DOMAIN.HIDDEN">srfi-54@xxxxxxxxxxxxxxxxx</a></li>
<li><em>User-agent</em>: Gnus/5.09 (Gnus v5.9.0) Emacs/21.3</li>
</ul>
<!--X-Head-of-Message-End-->
<!--X-Head-Body-Sep-Begin-->
<hr>
<!--X-Head-Body-Sep-End-->
<!--X-Body-of-Message-->
<pre>
It will work if you cut off the two supernumerary right parentheses of `cat
procedure' in
<a  rel="nofollow" href="http://srfi.schemers.org/srfi-54/post-mail-archive/msg00002.html">http://srfi.schemers.org/srfi-54/post-mail-archive/msg00002.html</a>.

;;; cat-lib.scm
(module cat mzscheme
	(require (lib &quot;include.ss&quot;)
		 (all-except (lib &quot;1.ss&quot; &quot;srfi&quot;)
			     append! reverse! map for-each member assoc)
		 (lib &quot;8.ss&quot; &quot;srfi&quot;)
		 (all-except (lib &quot;13.ss&quot; &quot;srfi&quot;)
			     string-upcase string-downcase string-titlecase))
	(provide cat)
	(include &quot;cat.scm&quot;))
;;; eof


If you want a more speed-up version, take a look at the following.

;;; srfi-54-lib.scm
(module srfi-54 mzscheme
	(require (lib &quot;include.ss&quot;)
		 (lib &quot;6.ss&quot; &quot;srfi&quot;)
		 (lib &quot;23.ss&quot; &quot;srfi&quot;))
	(provide cat)
	(include &quot;srfi-86.scm&quot;)		; SRFI-86
	(include &quot;srfi-54.scm&quot;))
;;; eof

;;; srfi-54.scm
;;; The cat uses alet*.
(define (str-index str char . start-end)
  (alet* ((len (string-length str))
	  (opt start-end
	       (start 0 (and (number? start) (&lt;= 0 start)))
	       (end len (and (number? end) (&lt;= start end len))))
	  ((n start) . lp))
    (and (&lt; n end)
	 (if (char=? char (string-ref str n))
	     n
	     (lp (+ n 1))))))

(define (every? pred ls)
  (let lp ((ls ls))
    (or (null? ls)
	(and (pred (car ls))
	     (lp (cdr ls))))))

(define (part pred ls)
  (let lp ((ls ls) (true '()) (false '()))
    (cond
     ((null? ls) (cons (reverse true) (reverse false)))
     ((pred (car ls)) (lp (cdr ls) (cons (car ls) true) false))
     (else (lp (cdr ls) true (cons (car ls) false))))))

(define (e-mold num pre)
  (let* ((str (number-&gt;string (exact-&gt;inexact num)))
	 (e-index (str-index str #\e)))
    (if e-index
	(string-append (mold (substring str 0 e-index) pre)
		       (substring str e-index (string-length str)))
	(mold str pre))))

(define (mold str pre)
  (let ((ind (str-index str #\.)))
    (if ind
	(let ((d-len (- (string-length str) (+ ind 1))))
	  (cond
	   ((= d-len pre) str)
	   ((&lt; d-len pre) (string-append str (make-string (- pre d-len) #\0)))
	   ;;((char&lt;? #\4 (string-ref str (+ 1 ind pre)))
	   ;;(alet ((com (expt 10 pre)))
	   ;;  (number-&gt;string (/ (round (* (string-&gt;number str) com)) com))))
	   ((or (char&lt;? #\5 (string-ref str (+ 1 ind pre)))
		(and (char=? #\5 (string-ref str (+ 1 ind pre)))
		     (or (&lt; (+ 1 pre) d-len)
			 (memv (string-ref str (+ ind (if (= 0 pre) -1 pre)))
			       '(#\1 #\3 #\5 #\7 #\9)))))
	    (apply
	     string
	     (let* ((minus (char=? #\- (string-ref str 0)))
		    (str (substring str (if minus 1 0) (+ 1 ind pre)))
		    (char-list
		     (reverse
		      (let lp ((index (- (string-length str) 1))
			       (raise #t))
			(if (= -1 index)
			    (if raise '(#\1) '())
			    (let ((chr (string-ref str index)))
			      (if (char=? #\. chr)
				  (cons chr (lp (- index 1) raise))
				  (if raise
				      (if (char=? #\9 chr)
					  (cons #\0 (lp (- index 1) raise))
					  (cons (integer-&gt;char
						 (+ 1 (char-&gt;integer chr)))
						(lp (- index 1) #f)))
				      (cons chr (lp (- index 1) raise))))))))))
	       (if minus (cons #\- char-list) char-list))))
	   (else
	    (substring str 0 (+ 1 ind pre)))))
	(string-append str &quot;.&quot; (make-string pre #\0)))))

(define (separate str sep num opt)
  (let* ((len (string-length str))
	 (pos (if opt
		  (let ((pos (remainder (if (eq? opt 'minus) (- len 1) len)
					num)))
		    (if (= 0 pos) num pos))
		  num)))
    (apply string-append
	   (let loop ((ini 0)
		      (pos (if (eq? opt 'minus) (+ pos 1) pos)))
	     (if (&lt; pos len)
		 (cons (substring str ini pos)
		       (cons sep (loop pos (+ pos num))))
		 (list (substring str ini len)))))))

(define (cat object . rest)
  (let* ((str-rest (part string? rest))
	 (str-list (car str-rest))
	 (rest-list (cdr str-rest)))
    (if (null? rest-list)
	(apply string-append
	       (cond
		((number? object) (number-&gt;string object))
		((string? object) object)
		((char? object) (string object))
		((boolean? object) (if object &quot;#t&quot; &quot;#f&quot;))
		(else
		 (get-output-string
		  (let ((str-port (open-output-string)))
		    (write object str-port)
		    str-port))))
	       str-list)
	(alet* ((cat rest-list
		     (width 0 (and (integer? width) (exact? width)))
		     (port #f (or (boolean? port) (output-port? port))
			   (if (eq? port #t) (current-output-port) port))
		     (char #\space (char? char))
		     (converter #f (and (pair? converter)
					(procedure? (car converter))
					(procedure? (cdr converter))))
		     (precision #f (and (integer? precision)
					(inexact? precision)))
		     (sign #f (eq? 'sign sign))
		     (radix 'decimal
			    (memq radix '(decimal octal binary hexadecimal)))
		     (exactness #f (memq exactness '(exact inexact)))
		     (separator #f (and (list? separator)
					(&lt; 0 (length separator) 3)
					(char? (car separator))
					(or (null? (cdr separator))
					    (let ((n (cadr separator)))
					      (and (integer? n) (exact? n)
						   (&lt; 0 n))))))
		     (writer #f (procedure? writer))
		     (pipe #f (and (list? pipe)
				   (not (null? pipe))
				   (every? procedure? pipe)))
		     (take #f (and (list? take)
				   (&lt; 0 (length take) 3)
				   (every? (lambda (x)
					     (and (integer? x) (exact? x)))
					   take))))
		(str
		 (cond
		  ((and converter
			((car converter) object))
		   (let* ((str ((cdr converter) object))
			  (pad (- (abs width) (string-length str))))
		     (cond
		      ((&lt;= pad 0) str)
		      ((&lt; 0 width) (string-append (make-string pad char) str))
		      (else (string-append str (make-string pad char))))))
		  ((number? object)
		   (and (not (eq? radix 'decimal)) precision
			(error &quot;cat: non-decimal cannot have a decimal point&quot;))
		   (and precision (&lt; precision 0) (eq? exactness 'exact)
			(error &quot;cat: exact number cannot have a decimal point without exact sign&quot;))
		   (let* ((exact-sign (and precision
					   (&lt;= 0 precision)
					   (or (eq? exactness 'exact)
					       (and (exact? object)
						    (not (eq? exactness
							      'inexact))))
					   &quot;#e&quot;))
			  (inexact-sign (and (not (eq? radix 'decimal))
					     (or (and (inexact? object)
						      (not (eq? exactness
								'exact)))
						 (eq? exactness 'inexact))
					     &quot;#i&quot;))
			  (radix-sign (cdr (assq radix
						 '((decimal . #f)
						   (octal . &quot;#o&quot;)
						   (binary . &quot;#b&quot;)
						   (hexadecimal . &quot;#x&quot;)))))
			  (plus-sign (and sign (&lt; 0 (real-part object)) &quot;+&quot;))
			  (exactness-sign (or exact-sign inexact-sign))
			  (str
			   (if precision
			       (let ((precision (inexact-&gt;exact
						 (abs precision)))
				     (imag (imag-part object)))
				 (if (= 0 imag)
				     (e-mold object precision)
				     (string-append
				      (e-mold (real-part object) precision)
				      (if (&lt; 0 imag) &quot;+&quot; &quot;&quot;)
				      (e-mold imag precision)
				      &quot;i&quot;)))
			       (number-&gt;string
				(cond
				 (inexact-sign (inexact-&gt;exact object))
				 (exactness
				  (if (eq? exactness 'exact)
				      (inexact-&gt;exact object)
				      (exact-&gt;inexact object)))
				 (else object))
				(cdr (assq radix '((decimal . 10)
						   (octal . 8)
						   (binary . 2)
						   (hexadecimal . 16)))))))
			  (str
			   (if (and separator
				    (not (or (and (eq? radix 'decimal)
						  (str-index str #\e))
					     (str-index str #\i)
					     (str-index str #\/))))
			       (let ((sep (string (car separator)))
				     (num (if (null? (cdr separator))
					      3 (cadr separator)))
				     (dot-index (str-index str #\.)))
				 (if dot-index
				     (string-append
				      (separate (substring str 0 dot-index)
						sep num (if (&lt; object 0)
							    'minus #t))
				      &quot;.&quot;
				      (separate (substring
						 str (+ 1 dot-index)
						 (string-length str))
						sep num #f))
				     (separate str sep num (if (&lt; object 0)
							       'minus #t))))
			       str))
			  (pad (- (abs width)
				  (+ (string-length str)
				     (if exactness-sign 2 0)
				     (if radix-sign 2 0)
				     (if plus-sign 1 0))))
			  (pad (if (&lt; 0 pad) pad 0)))
		     (if (&lt; 0 width)
			 (if (char-numeric? char)
			     (if (&lt; (real-part object) 0)
				 (string-append (or exactness-sign &quot;&quot;)
						(or radix-sign &quot;&quot;)
						&quot;-&quot;
						(make-string pad char)
						(substring str 1
							   (string-length
							    str)))
				 (string-append (or exactness-sign &quot;&quot;)
						(or radix-sign &quot;&quot;)
						(or plus-sign &quot;&quot;)
						(make-string pad char)
						str))
			     (string-append (make-string pad char)
					    (or exactness-sign &quot;&quot;)
					    (or radix-sign &quot;&quot;)
					    (or plus-sign &quot;&quot;)
					    str))
			 (string-append (or exactness-sign &quot;&quot;)
					(or radix-sign &quot;&quot;)
					(or plus-sign &quot;&quot;)
					str
					(make-string pad char)))))
		  (else
		   (let* ((str (cond
				(writer (get-output-string
					 (alet ((str-port
						 (open-output-string)))
					   (writer object str-port)
					   str-port)))
				((string? object) object)
				((char? object) (string object))
				((boolean? object) (if object &quot;#t&quot; &quot;#f&quot;))
				(else (get-output-string
				       (let ((str-port (open-output-string)))
					 (write object str-port)
					 str-port)))))
			  (str (if pipe
				   (let loop ((str ((car pipe) str))
					      (fns (cdr pipe)))
				     (if (null? fns)
					 str
					 (loop ((car fns) str)
					       (cdr fns))))
				   str))
			  (str
			   (if take
			       (let ((left (car take))
				     (right (if (null? (cdr take))
						0 (cadr take)))
				     (len (string-length str)))
				 (define (substr str beg end)
				   (let ((end (cond
					       ((&lt; end 0) 0)
					       ((&lt; len end) len)
					       (else end)))
					 (beg (cond
					       ((&lt; beg 0) 0)
					       ((&lt; len beg) len)
					       (else beg))))
				     (if (and (= beg 0) (= end len))
					 str
					 (substring str beg end))))
				 (string-append
				  (if (&lt; left 0)
				      (substr str (abs left) len)
				      (substr str 0 left))
				  (if (&lt; right 0)
				      (substr str 0 (+ len right))
				      (substr str (- len right) len))))
			       str))
			  (pad (- (abs width) (string-length str))))
		     (cond
		      ((&lt;= pad 0) str)
		      ((&lt; 0 width) (string-append (make-string pad char) str))
		      (else (string-append str (make-string pad char))))))))
		(str (apply string-append str str-list)))
	  (and port (display str port))
	  str))))
;;; eof

-- 
Joo ChurlSoo


</pre>
<!--X-Body-of-Message-End-->
<!--X-MsgBody-End-->
<!--X-Follow-Ups-->
<hr>
<!--X-Follow-Ups-End-->
<!--X-References-->
<!--X-References-End-->
<!--X-BotPNI-->
<ul>
<li>Prev by Date:
<strong><a href="msg00004.html">Reference Implementation problem</a></strong>
</li>
<li>Next by Date:
<strong><a href="msg00006.html">Re: Reference Implementation problem</a></strong>
</li>
<li>Previous by thread:
<strong><a href="msg00004.html">Reference Implementation problem</a></strong>
</li>
<li>Next by thread:
<strong><a href="msg00006.html">Re: Reference Implementation problem</a></strong>
</li>
<li>Index(es):
<ul>
<li><a href="maillist.html#00005"><strong>Date</strong></a></li>
<li><a href="threads.html#00005"><strong>Thread</strong></a></li>
</ul>
</li>
</ul>

<!--X-BotPNI-End-->
<!--X-User-Footer-->
<!--X-User-Footer-End-->
</body>
</html>
